#!/usr/bin/perl -w

use strict;
use Getopt::Long;

### command-line options
my @o_cues = ();
my %o_cues = ();

GetOptions( "cue=s" => \@o_cues );

for my $x ( split(/;/,join(";",@o_cues)) )
{
    my ($pos,$b0,$b1) = split(/,/,$x);

    $o_cues{$pos}{$b0} = 0;
    $o_cues{$pos}{$b1} = 1;
}

### command-line usage
if( @ARGV == 0 )
{
    print "usage: $0 markers1.csv markers2.csv ...\n";
    print "\n";
    print "options:\n";
    print "  --cues\tmarker sites (eg. --cue 77,T,C --cue 153,T,A --cue 279,G,T ...)\n";
    print "\n";
    exit;
}

### command-line arguments
my @files = @ARGV;

my %stat = ();
my %freq = ();

my @sorted = sort { $a <=> $b } keys %o_cues;

for my $file ( @files )
{
    ### load precomputed markers
    my $data = load_zmw_data($file);
    
    for my $movie ( keys %$data )
    {
	for my $zmw ( sort { $a <=> $b } keys %{$$data{$movie}} )
	{
	    ### {0,1} vector
	    my @cross = map { $o_cues{$_}{$$data{$movie}{$zmw}{$_}} } @sorted;
	    
	    ### count chimera switch points
	    my $cnum = 0;
	    
	    for( my $i = 1; $i < @cross; $i++ )
	    {
		if( $cross[$i-1] ne $cross[$i] )
		{
		    $cnum++;
		}
	    }
	    
	    ### cnum statistics
	    $stat{$cnum}++;

	    ### count intervals
	    for( my $i = 1; $i < @cross; $i++ )
	    {
		if( $cross[$i-1] ne $cross[$i] )
		{
		    my $cue = $sorted[$i];
		    
		    $freq{$cue}{"start"} = $sorted[$i-1];
		    $freq{$cue}{"end"} = $sorted[$i];
		    $freq{$cue}{"count"}++;
		}
	    }
	}
    }
}

my $total_reads = 0;

for my $cnum ( keys %stat )
{
    $total_reads += $stat{$cnum};
}

print "Recombinations,Reads,Fraction\n";
for my $cnum ( sort { $a <=> $b } keys %stat )
{
    printf( "%i,%i,%.7f\n",
	    $cnum,
	    $stat{$cnum},
	    $stat{$cnum}/$total_reads );
}

my $total_switches = 0;

for my $cue ( sort { $a <=> $b } keys %freq )
{
    my $interval_length = $freq{$cue}{"end"} - $freq{$cue}{"start"};
    $freq{$cue}{"norm"} = $freq{$cue}{"count"} / $interval_length;
    $total_switches += $freq{$cue}{"norm"};
}

print "Start,End,Percentage\n";
for my $cue ( sort { $a <=> $b } keys %freq )
{
    printf( "%i,%i,%.7f\n",
	    $freq{$cue}{"start"},
	    $freq{$cue}{"end"},
	    $freq{$cue}{"norm"}/$total_switches );
}

sub load_zmw_data {
    my ($file) = @_;
    
    my %zmws = ();
    my @head = ();

    open(IN,$file) || die "Can't open '$file': $!";
    
    while( my $line = <IN> )
    {
	chomp($line);
	
	### parse line
	my @tokens = split(/,/,$line);
	
	if( @head == 0 )
	{
	    ### extract column names
	    @head = @tokens;
	}
	else
	{
	    ### store data fields
	    my %entry = ();
	    
	    for( my $i = 0; $i < @head; $i++ )
	    {
		$entry{$head[$i]} = $tokens[$i];
	    }

	    $zmws{$entry{"Movie"}}{$entry{"ZMW"}} = \%entry;
	}
    }

    close(IN);

    return \%zmws;
}
